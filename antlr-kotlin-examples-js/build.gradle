buildscript {
    ext.kotlin_version = '1.5.21'
    ext.projectVersion = '0.0.7'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.craigburke.gradle:karma-gradle:1.4.4"
        classpath "com.strumenta.antlr-kotlin:antlr-kotlin-gradle-plugin:$projectVersion"
    }
}

plugins {
    id 'org.jetbrains.kotlin.js' version '1.5.21'
    //id("com.strumenta.antlr-kotlin") version "0.0.7"
}

apply plugin: 'idea'
apply plugin: 'com.craigburke.karma'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "com.strumenta.antlr-kotlin:antlr-kotlin-runtime-js:$projectVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

kotlin {
    js(BOTH) {
        browser {
        }
        nodejs {
        }
    }
}

generateKotlinGrammarSource {
    maxHeapSize = "64m"
    //arguments += ['-package', 'me.tomassetti.minicalc']
    outputDirectory = new File("generated-src/antlr/".toString())
}
compileKotlin2Js.dependsOn generateKotlinGrammarSource
sourceSets {
    generated {
        kotlin.srcDir 'generated-src/antlr/'
    }
}
compileKotlin2Js.source sourceSets.generated.kotlin, sourceSets.main.kotlin

clean{
    delete "generated-src"
}

idea {
    module {
        sourceDirs += file("generated-src/antlr/")
    }
}

def libDir = "$buildDir/lib"
def compileOutput = compileKotlin2Js.outputFile
def testOutput = compileTestKotlin2Js.outputFile

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into libDir
}

karma {
    dependencies(['mocha'])

    frameworks = ['mocha']
    browsers = ['PhantomJS']

    files = [
            "$libDir/kotlin.js",
            "$libDir/*.js",
            compileOutput,
            testOutput
    ]
}

karmaRun {
    dependsOn compileTestKotlin2Js
    dependsOn populateNodeModules
}

test.dependsOn karmaRun
clean.dependsOn karmaClean

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/js"

    dependsOn classes
}



assemble.dependsOn assembleWeb
